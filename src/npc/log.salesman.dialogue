~ NPC_start
if Storage.sf.talked_once_merchant
	=> NPC_trade
...
Welcome to my shop,[wait=0.2] traveler. 
I sell fresh breaths,[wait=0.2] harvested at dawn.
Hah!
I joke,[wait=0.2] traveler. I have a collection of fine rods for sale. Each one able to cast further than before. Catch new fish,[wait=0.2] yes?
Good.
All I require is a winning race ticket.
Bring me two tickets. For each ticket I give you a new fine rod.
set Storage.sf.talked_once_merchant = true
do Storage.save_process()
=> END

~ NPC_trade
if Storage.sf.rods_recieved > 0
	Nice to see you,[wait=0.2] traveler.

if Storage.sf.rods_recieved < 2
	Welcome back,[wait=0.2] traveler.
else
	Hello,[wait=0.2] fisherman.

if Storage.sf.tickets == 0 and Storage.sf.rods_recieved == 0
	...
	No tickets? That's a shame.
	=> END

if Storage.sf.tickets > 0 and Storage.sf.rods_recieved == 0
	Ahh.. you must be a fellow skilled fisherman.
	I got a new rod for ya.
	Beautiful,[wait=0.2] isn't she? (Will increase cast to 10 meters)
	- Sacrifice your winning ticket for the rod
		W-wow! T..t-thank you.
		Ahem.. it's been a while since I've had a customer
		set Storage.sf.rods_recieved += 1
		set Storage.sf.tickets -= 1
		do Storage.sf.inventory.append(Storage.ROD_10M)
		do Storage.save_process()
		=> END
	- Walk away
		...
		I know you'll be back.. A skilled fisherman like yourself can't resist a good upgrade.
		=> END

if Storage.sf.tickets > 0 and Storage.sf.rods_recieved == 1
	Ah! You won again. With the help of my rod no doubt!
	This.. is my finest work. (Will increase cast to 20 meters)
	- Sacrifice your winning ticket for the rod
		Let me... Tuck this awa first.
		Thank you,[wait=0.2] pleasure doing business with you.
		set Storage.sf.rods_recieved += 1
		set Storage.sf.tickets -= 1
		do Storage.sf.inventory.append(Storage.ROD_20M)
		do Storage.save_process()
		=> END
	- Walk away
		So be it. Greed eats away at us. It's nice to see someone like you for a change.
		=> END

=> END